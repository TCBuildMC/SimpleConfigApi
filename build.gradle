plugins {
    id "java-library"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

group = rootProject.maven_group
version = rootProject.release_version
base.archivesName = project.archives_base_name

repositories {
    maven {
        url = "https://maven.quiltmc.org/repository/release/"
    }
    mavenCentral()
}

dependencies {
    annotationProcessor(compileOnly("org.projectlombok:lombok:1.18.34"))
//    annotationProcessor(compileOnly("com.google.auto.service:auto-service:1.1.1"))
    compileOnly "org.jetbrains:annotations:24.1.0"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.16.0"
//    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.17.2"
//    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-toml:2.17.2"
//    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.17.2"
    implementation "com.google.code.gson:gson:2.11.0"
    implementation "com.alibaba.fastjson2:fastjson2:2.0.52"
    implementation "blue.endless:jankson:1.2.3"
//    implementation "org.quiltmc:quilt-json5:1.0.4+final"
//    implementation "org.dom4j:dom4j:2.1.4"
    implementation "org.yaml:snakeyaml:2.2"
    implementation "com.moandjiezana.toml:toml4j:0.7.2"

    implementation "commons-io:commons-io:2.16.1"

    testImplementation platform('org.junit:junit-bom:5.10.3')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testAnnotationProcessor(testCompileOnly("org.projectlombok:lombok:1.18.34"))
}

test {
    useJUnitPlatform()

    ignoreFailures true

    testLogging {
        exceptionFormat = "full"
        events "passed", "skipped", "failed"
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile).configureEach { tsk ->
    tsk.options.encoding = "UTF-8"
}

tasks.withType(Test).configureEach { tsk ->
    tsk.systemProperty "file.encoding", "UTF-8"
}

jar {
    manifest {
        attributes "Implementation-Version": project.version
    }
}

compileJava.dependsOn clean

shadowJar {
    archiveClassifier.set "shaded"

    def reloc = { String from, String to ->
        relocate from, to
    }

    reloc "com.fasterxml.jackson", "xyz.tcbuildmc.common.config.v0.lib.jackson"
    reloc "com.google.gson",       "xyz.tcbuildmc.common.config.v0.lib.gson"
    reloc "com.google.errorprone", "xyz.tcbuildmc.common.config.v0.lib.errorprone"
    reloc "com.alibaba.fastjson2", "xyz.tcbuildmc.common.config.v0.lib.fastjson2"
    reloc "blue.endless.jankson",  "xyz.tcbuildmc.common.config.v0.lib.jankson"
    reloc "org.yaml.snakeyaml",    "xyz.tcbuildmc.common.config.v0.lib.snakeyaml"
    reloc "com.moandjiezana.toml", "xyz.tcbuildmc.common.config.v0.lib.toml4j"
    reloc "org.apache.commons.io", "xyz.tcbuildmc.common.config.v0.lib.commons_io"

    exclude "schema/**"
}

assemble.dependsOn shadowJar

publishing {
    publications {
        create("MavenJava", MavenPublication) {
            from components.java
            artifactId = project.archives_base_name
        }
    }

    repositories {
        mavenLocal()
    }
}
